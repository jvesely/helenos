interface vfs extends service {
		/* Establish connection */
		ipcarg_t ipc_m_connect_me_to(void);
		
		/* Register a filesystem driver */
		ipcarg_t register(in_copy string name);
		
		/* Mount filesystem */
		ipcarg_t mount(in ipcarg_t device, in ipcarg_t flags, in_copy string point, in_copy string opts, in_copy string fs);
		
		/* Open file */
		ipcarg_t open(in ipcarg_t lflag, in ipcarg_t oflag, in ipcarg_t mode, in_copy string path, out ipcarg_t fd);
		
		/* Open file using node */
		ipcarg_t open_node(in ipcarg_t fs_handle, in ipcarg_t dev_handle, in ipcarg_t index, in ipcarg_t oflag, out ipcarg_t fd);
		
		/* Read data from file */
		ipcarg_t read(in ipcarg_t fd, out_copy stream data);
		
		/* Write data to file */
		ipcarg_t write(in ipcarg_t fd, in_copy stream data);
		
		/* Seek in file */
		ipcarg_t seek(in ipcarg_t fd, in ipcarg_t offset, in ipcarg_t whence);
		
		/* Truncate file */
		ipcarg_t truncate(in ipcarg_t fd, in ipcarg_t size);
		
		/* Get file metadata */
		ipcarg_t fstat(in ipcarg_t fd, out_copy stream stat);
		
		/* Get directory entry metadata */
		ipcarg_t stat(in_copy string path, out_copy stream stat);
		
		/* Create directory */
		ipcarg_t mkdir(in ipcarg_t mode, in_copy string path);
		
		/* Delete directory entry */
		ipcarg_t unlink(in ipcarg_t lflag, in_copy string path);
		
		/* Rename directory entry */
		ipcarg_t rename(in_copy string old, in_copy string new);
		
		/* Flush file buffers */
		ipcarg_t sync(in ipcarg_t fd);
		
		/* In-protocol status value */
		ipcarg_t ipc_m_ping(void);
		
		/* Close connection */
		ipcarg_t ipc_m_phone_hungup(void);
	protocol:
		[vfs.bp]
};

interface fs extends service {
		/* Establish connection */
		ipcarg_t ipc_m_connect_me_to(void);
		
		/* Notify filesystem that it was mounted */
		ipcarg_t mounted(in ipcarg_t dev_handle, in_copy string opts);
		
		/* Mount filesystem */
		ipcarg_t mount(in ipcarg_t device, in ipcarg_t flags, in_copy string point, in_copy string opts, ...);
		
		/* Open file by node */
		ipcarg_t open_node(in ipcarg_t lflag, in ipcarg_t oflag, in ipcarg_t mode, ...);
		
		/* Lookup file */
		ipcarg_t lookup(in ipcarg_t lflag, in ipcarg_t oflag, in ipcarg_t mode, ...);
		
		/* Read data from file */
		ipcarg_t read(in ipcarg_t dev_handle, in ipcarg_t fs_index, in ipcarg_t offset, out_copy stream data);
		
		/* Write data to file */
		ipcarg_t write(in ipcarg_t dev_handle, in ipcarg_t fs_index, in ipcarg_t offset, out_copy stream data);
		
		/* Truncate file */
		ipcarg_t truncate(in ipcarg_t dev_handle, in ipcarg_t fs_index, in ipcarg_t size);
		
		/* Get directory entry metadata */
		ipcarg_t stat(in ipcarg_t dev_handle, in ipcarg_t fs_index, out_copy stream stat);
		
		/* Flush file buffers */
		ipcarg_t sync(in ipcarg_t dev_handle, in ipcarg_t fs_index);
		
		/* Notify on file close */
		ipcarg_t close(in ipcarg_t dev_handle, in ipcarg_t fs_index);
		
		/* Close connection */
		ipcarg_t ipc_m_phone_hungup(void);
};

frame vfs_manager {
	provides:
		vfs vfs;
	requires:
		[/uspace/lib/libc/requires%]
		fs fs;
		ns ns;
	protocol:
		[/uspace/lib/libc/protocol] +
		[vfs_server.bp]
};

architecture vfs {
	inst vfs_manager vfs;
	inst tmpfs tmpfs;
	inst fat fat;
	inst devfs devfs;
	
	bind vfs:fs to tmpfs:fs;
	bind vfs:fs to fat:fs;
	bind vfs:fs to devfs:fs;
	
	bind tmpfs:vfs to vfs:vfs;
	bind fat:vfs to vfs:vfs;
	bind devfs:vfs to vfs:vfs;
	
	delegate vfs to vfs:vfs;
	
	[/uspace/lib/libc/subsume%vfs]
	[/uspace/lib/libc/subsume%tmpfs]
	[/uspace/lib/libc/subsume%fat]
	[/uspace/lib/libc/subsume%devfs]
	
	subsume vfs:ns to ns;
	subsume tmpfs:ns to ns;
	subsume fat:ns to ns;
	subsume devfs:ns to ns;
	
	subsume tmpfs:bd to bd;
	subsume fat:bd to bd;
	
	subsume devfs:devmap_client to devmap_client;
	subsume devfs:device to device;
};
